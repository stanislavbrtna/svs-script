function main {
  sys print("AUTOTEST\nSTART-####");
  cnt = 1;
  if (1 == 1){
    sys print(cnt + " - if:OK");
  } else {
    sys print(cnt + " - else:ERR");
  }
  cnt = cnt + 1;

  if (1 != 1) {
    sys print(cnt + " - if:ERR");
  }else{
    sys print(cnt + " - else:OK");
  }
  cnt = cnt + 1;

  FAIL = 0;

  a = 5;
  b = 10;
  {
  	local a;
    a = 15;
    b = a;
  }

  if((a == 5) * (b == 15)) {
    sys print(cnt + " - local:OK");
  }else{
    sys print(cnt + " - local:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  i_flt = 9.0;
  i2_flt = 2.0;
  n_flt = i_flt / i2_flt;

  if(n_flt != 4.5) {
    opFAIL = 1;
  }

  if(((i_flt + i2_flt) != 11.0) + ((i_flt-i2_flt) != 7.0) + ((i_flt*i2_flt) != 18.0)) {
    opFAIL = 1;
  }

  if("alpha" + "beta" + 8 != "alphabeta8") {
    opFAIL = 1;
  }

  if(((6+5)!=11)+((8-1)!=7)+((9*2)!=18)+(9/2!=4)+(9%2!=1)){
    opFAIL = 1;
  }

  if(opFAIL == 1) {
    FAIL = 1;
    sys print(cnt + " - math op: FAIL");
  } else {
    sys print(cnt + " - math op: OK");
  }
  cnt = cnt + 1;

  if (("str" == ret_str()) * (5.5 == ret_flt()) * (8 == ret_val())) {
    sys print(cnt + " - call return:OK");
  } else {
    sys print(cnt + " - call return:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  a = 0;
  while(a < 5) {
    a = a + 1;
  }

  if (a != 5) {
    sys print(cnt + " - while:FAIL");
    FAIL = 1;
  } else {
    sys print(cnt + " - while:OK");
  }
  cnt = cnt + 1;

  a = 0;
  b = 0;
  while(a < 50) {
    a = a + 1;
    if (a == 25) {
      break;
    }
  }

  if (a != 25) {
    sys print(cnt + " - while-break:FAIL");
    FAIL = 1;
  } else {
    sys print(cnt + " - while-break:OK");
  }
  cnt = cnt + 1;

  b = 0;
  for(a = 0; a < 5; a = a + 1;) {
    b = 5;
  }

  if ((a != 5) + (b != 5)) {
    sys print(cnt + " - for:FAIL");
    FAIL = 1;
  } else {
    sys print(cnt + " - for:OK");
  }
  cnt = cnt + 1;

  stnd = 0;
  standalone();
  if(stnd) {
    sys print(cnt + " - standalone call:OK");
  } else {
    sys print(cnt + " - standalone call:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  a = 0;
  argsfunc(1, 2, 3, 4, 5, 6, "karel", 5.5);
  if (a == 55) {
    sys print(cnt + " - standalone argumented call:OK");
  } else {
    sys print(cnt + " - standalone argumented call:FAIL");
    FAIL=1;
  }
  cnt = cnt + 1;

  if ((99 == argsfunc(1, 2, 3, 4, 5, 6, "karel", 5.5)) * (1 != 6)) {
    sys print(cnt + " - argumented call:OK");
  } else {
    sys print(cnt + " - argumented call:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  if ((TEST_VAL == 5) * (TEST_VAL2 == 8)) {
    sys print(cnt + " - Constants:OK");
  } else {
    sys print(cnt + " - Constants:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;


  LogicFail = 0;
  if (not 1) {
  	LogicFail = 1;
  }

  if (((1 and 1) != 1)+((1 and 0)!=0)) {
  	LogicFail = 1;
  }

  if (((0 or 1) != 1)+((0 or 0)!=0)) {
  	LogicFail = 1;
  }
  if (LogicFail == 0){
  	sys print(cnt + " - Logic functions:OK");
  }else{
  	sys print(cnt + " - Logic functions:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;


  if (typeof(1) == TYPE_NUM
  		and typeof("") == TYPE_STR
  		and typeof(3.3) == TYPE_FLOAT
  		and num("69") == 69
  		and num(55.96) == 55
  		and float("3.3") == 3.3
  		and float(99) == 99.0
  		and isnum("da5sd") == 0
  		and isnum("1") == 1
  		and isnum("1.1") == 2
  		and isnum(1) == 1
  		and isnum(1.1) == 2
  		and len("abcd") == 4
  		and getcp("abcd", 2) == "b") {

    sys print(cnt + " - Integrated functions:OK");
  }else{
    sys print(cnt + " - Integrated functions:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  for(x = 0; x < 10; x++;) {
  	b = 1 + b;
  }
  for(y = 10; y > 0; y--;) {}

  if (x == 10 and y == 0){
    sys print(cnt + " - Increment and decrement:OK");
  }else{
    sys print(cnt + " - Increment and decrement:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  if ("megabox123" == &megabox123) {
    sys print(cnt + " - identificator passing:OK");
  }else{
    sys print(cnt + " - identificator passing:FAIL");
    FAIL = 1;
  }
  cnt = cnt + 1;

  #comments hopefully works
  if(FAIL) {
    sys print("Test FAILED");
  } else {
    sys print("Test PASSED");
  }

  return FAIL;
}

function ret_str{
  return "str";
}

function ret_flt{
  return 5.5;
}

function ret_val{
  return 8;
}

function standalone{
  stnd = 1;
}

function argsfunc {
  if((arg0 == 1)*(arg1 == 2)*(arg2 == 3)*(arg3 == 4)*(arg4 == 5)*(arg5 == 6)*(arg6 == "karel")*(arg7 == 5.5)) {
    a = 55;
    return 99;
  } else {
    sys print ("ko: "+arg0+" "+arg1+" "+arg2+" "+arg3+" "+arg4+" "+arg5+" "+arg6+" "+arg7);
    return 0;
  }
}

